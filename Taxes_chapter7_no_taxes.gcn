# ############################################################################
# Taxes Model. Chapter 7, Torres (2015) - NO TAXES version with preference shock
# Modified by Ana Karla, 2025
# ############################################################################

options
{
    output logfile = TRUE;
    output LaTeX = TRUE;
};

tryreduce
{
    K_d[], L_d[];
};

block CONSUMER
{
    definitions
    {
        u[] = gamma*log(D[] * C[]) + (1-gamma)*log(1 - L_s[]);
    };
    controls
    {
        K_s[], C[], L_s[], I[];
    };
    objective
    {
        U[] = u[] + beta * E[][U[1]];
    };
    constraints
    {
        I[] + C[] = r[] * K_s[-1] + W[] * L_s[] + pi[] : lambda_c[];
        K_s[] =  I[] + (1 - delta) * K_s[-1];
    };
    calibration
    {
        delta = 0.06;
        beta = 0.97;
        gamma = 0.40;
    };
};

block FIRM
{
    controls
    {
        K_d[], L_d[], Y[], pi[];
    };
    objective
    {
        PI[] = pi[];
    };
    constraints
    {
        Y[] = A[] * K_d[]^alpha * L_d[]^(1 - alpha);
        pi[] = Y[] - L_d[] * W[] - r[] * K_d[];
    };
    calibration
    {
        alpha = 0.350;
    };
};

block EQUILIBRIUM
{
    identities
    {
        K_d[] = K_s[-1];
        L_d[] = L_s[];
    };
};

block EXOG
{
    identities
    {
        A[] = exp(rho * log(A[-1]) + epsilon_A[]);
    };
    shocks
    {
        epsilon_A[];
    };
    calibration
    {
        rho = 0.95;
    };
};

block EXOG_CONSUMPTION
{
    identities
    {
        D[] = exp(rho_D * log(D[-1]) + epsilon_D[]);
    };
    shocks
    {
        epsilon_D[];
    };
    calibration
    {
        rho_D = 0.99;
    };
};
